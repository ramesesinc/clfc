import com.rameses.annotations.*;
import com.rameses.common.*;
import com.rameses.services.extended.*;
import java.rmi.server.UID;

class NewLoanAppService 
{	
	@ActiveDB(value='loanapp')
	def loanapp;

	@ActiveDB(value='loanapp_borrower')
	def loanapp_borrower; 	

	@ActiveDB(value='loan_product_type')
	def productType;

	@ActiveDB(value='borrower')
	def borrower; 	

	@Service('DateService') 
	def dateSvc;

	@Service('SequenceService') 
	def seqSvc;

	@Env 
	def env;

	@ProxyMethod 
	public def initEntity() { 
        def entity = [
            objid: 'LOAN' + new UID(), 
            borrower: [:], 
            schedule: [:], 
            branch: [:], 
            productTypes: productType.getList()  
        ]; 	
        return entity; 	
	} 

	@ProxyMethod 
	public def create(data) { 	
		if (data.clienttype == 'MARKETED' && !data.marketedby) 
			throw new Exception('Please specify marketedby'); 

		data.branch = [code: env.ORGID, name: env.ORGNAME];
		data.dtcreated = dateSvc.serverDate.toString();
		data.createdby = env.USERID; 
		data.state = 'INCOMPLETE'; 
		data.version = 1;

		def seqno = seqSvc.getNextSeries('loan'); 
		data.loanno = data.branch.code + addLeadingChars(seqno,'0',8); 
		data.appno = data.loanno + addLeadingChars(data.version,'0',2); 
		data = loanapp.create(data); 

		buildSearchIndex(data); 
		buildBorrower(data);
		return data; 
	} 

	private void buildSearchIndex(data) { 
		def name = new StringBuffer();
		name.append(data.borrower.lastname + ', ' + data.borrower.firstname); 
		if (data.borrower.middlename) name.append(' ' + data.borrower.middlename);

		data.fullborrowername = name.toString();
		data.dtposted = data.dtcreated; 
		data.postedby = data.createdby;
		data.branchcode = data.branch.code;
		data.branchname = data.branch.name;
		loanapp.create(data, 'search_index'); 
	}

	private void buildBorrower(data) { 
		def o = borrower.findByObjid([objid: data.borrower.objid]); 
		if (o == null) { 
			borrower.create(data.borrower); 
		} else {
			o.putAll(data.borrower); 
			borrower.update(o); 
		}
		
		o = [
			objid: 		 'LB' + new UID(), 
			parentid: 	 data.objid, 
			borrowerid:  data.borrower.objid, 
			principalid: data.borrower.objid,
			type:        'PRINCIPAL' 
		];
		loanapp_borrower.create(o); 

		o = [
			objid: 'BIDX' + new UID(), 
			loanappid: data.objid, 
			loanappno: data.appno, 
			borrowerid: data.borrower.objid, 
			borrowername: data.borrower.name, 
			borrowertype: 'PRINCIPAL' 
		];
		loanapp.create(o, 'borrower_index'); 
	}	

	private String addLeadingChars(value, schar, length) {
		def buffer = new StringBuffer();
		def sval = (value == null? "": value.toString()); 
		def len = length-sval.length();
		for (int i=0; i<len; i++) {
			buffer.append(schar); 
		} 
		buffer.append(sval);
		return buffer.toString(); 
	} 
}
